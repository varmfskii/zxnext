\chapter{Interrupts}
Development

[14\_1] TSconf has an extended system of interrupts that can be
triggered if there are such states as: beam arrival at a given screen
position, beam arrival at the beginning of a line, line display on the
screen, completion of data transfer.

Tao says: The system has three types of masked interrupts that can be
called at an address that has a high byte — the address in register I,
and the youngest — its type:
\begin{enumerate}
\item \#FF - frame
\item \#FD - lowercase (Line)
\item \#FB - DMA.
\end{enumerate}
The processing of these interrupts can be switched by the INTMask port
(\#2A af), changing the state of the bits:

0 - Frame, 1 - Line, 2 - DMA, which leads to an on / off call to
handlers.

State of bits: 0 - disabled / 1 - enabled.

In the case of the arrival of several events at the same time, the
interrupt with the lower number will be processed first.

How can we manage these capabilities? Consider in order:

Frame A

frame interrupt in the standard Spectrum comes at the very beginning
of the screen drawing. For TSconf, this parameter can be set using
three ports:
\begin{itemize}
\item VSINT: the high and low bytes of these two ports (VSINTH -
  \#24af, VSINTL - \#23af) allow you to specify the vertical position
  of the beam, which allows us to specify the entire area of the
  screen lines: 0 - 319;
\item HSINT: port indicating the horizontal position of the beam
  (position in the line): 0–223 in 3.5 MHz cycles,
\end{itemize}
If the position of the beam coincides, a frame interrupt handler will
be called. If you specify values outside the specified range, the
interrupt will not be generated.

So let's give an example in which the frame interrupt handler \#BE will
be installed:
\begin{verbatim}
im2_init
                xor a
                ld bc,HSINT
                out (c),a
                ld bc,VSINTL
                out (c),a
                ld bc,VSINTH
                out (c),a

                ld a,#be
                ld i,a
                ld hl,int
                ld (#beff),hl
                im 2
                ret
\end{verbatim}
In this case, the positions for calling the frame interrupt handler
were set to 0 (upper left corner of the screen, leftmost position).
When the ray of the screen rendering arrives at this position, the
transition will be made to the personnel interrupt handler code by the
int tag.

In this case, this handler will be called with a frame pulse frequency
of 50 Hz.

And if we need more?

Consider the situation when we need to call the personnel handler at
the beginning of the screen, and at the beginning of the line at
number 128.

Why? So that the upper part of the screen was colored red!

To do this, we will need to specify when we need to call the handler
in each next time. In the handler, the position of the screen 0 (the
first line of the screen) will indicate in which place of the screen
the following personnel interruption will be triggered:
\begin{verbatim}
int
                push af
                push bc
                push hl
                ld a,128
                ld bc,VSINTL
                out (c),a
                xor a
                ld bc,VSINTH
                out (c),a
                ld hl,int128
                ld (#beff),hl
                ld a,#f2
                ld bc,BORDER
                out (c),a
                pop hl
                pop bc
                pop af
                ei
                ret
\end{verbatim}
Here we indicate that the next personnel interrupt should be called in
the vertical position of the beam on line 128, and the handler will be
the int128 subroutine. Set the red color from the standard zx spectrum
palette (palette \#0f, color 2 - for the border color, simply specify
the desired color from the entire 256-cell palette).

When the beam hits the beginning of line 128, the subroutine int128 is
called:
\begin{verbatim}
int128
                push af
                push bc
                push hl
                xor a
                ld bc,VSINTL
                out (c),a
                ld bc,VSINTH
                out (c),a
                ld hl,int
                ld (#beff),hl
                ld a,#f0
                ld bc,BORDER
                out (c),a
                pop hl
                pop bc
                pop af
                ei
                ret
\end{verbatim}
in which we indicated that the next personnel interruption will be
processed by the int subroutine at the beam position 0, and set the
border color to 0 of the standard palette.

We note that the HSINT horizontal position port does not change now
and is set to 0, therefore the call to the handlers is always at the
beginning of the line. I suggest you play with it yourself :)

Great, we can already get to the right place on the screen. But what
if you need a certain subroutine to execute EVERY line?  To do this,
we have a tool in the form of a string interrupt.

Line interrupts

This type of interrupt will call its handler when the beam is at
position 0 of each line of the screen (whether it is visible or not on
the screen is not important for the system).

To use it, you must do the following:
\begin{itemize}
\item set the address of the line interrupt handler
\item allow INTMASK port processing (\#2Aaf)
\end{itemize}
So, turn on the bits of the INTMASK port, which allow processing of
both personnel and line interrupts, set the address of the handler:
\begin{verbatim}
                ld      hl,line_proc
                ld      (#befd),hl

                ld      bc,INTMASK
                ld      a,%00000011
                out     (c),a
\end{verbatim}
We note to ourselves that both personnel and line interrupts are now
allowed.  The first handler will be processed with a smaller number -
i.e. first personnel, then lower case.

Change the task. Let each line on the border be visible strip of a
different color.

Disable the frame handler, leaving only the lowercase one:
\begin{verbatim}
                ld      hl,line_proc
                ld      (#befd),hl

                ld      bc,INTMASK
                ld      a,%00000010
                out     (c),a
\end{verbatim}
Add a line interrupt handler:
\begin{verbatim}
line_proc
                exx
                ex af, af'
lp1             ld a,#ff
                inc a
                ld (lp1+1),a
                or #f0
                ld bc,BORDER
                out (c),a
                ex af, af'
                exx
                ei
                ret
\end{verbatim}
Since this handler will be called very often, it is worth minimizing
the processor clock used by switching to an alternate set of
registers. Then we increase the color number counter in the palette
and send it to the system (border port \#0Faf).

The peculiarity of these interrupt systems is that using the HALT
“stop the processor” command so familiar to us will only wait for the
next personnel / line interrupt, that is, for the first example,
drawing a full screen takes 2 HALTs and 320 HALT for a lowercase :)

DMA

Handler to complete the transfer will be called after the transfer is
completed. Your KO ;)

Turn on its processing and specify the address of the handler. As
usual:
\begin{verbatim}
                ld      hl,dma_proc
                ld      (#befb),hl

                ld      bc,INTMASK
                ld      a,%00000100
                out     (c),a
\end{verbatim}
Note that in this case, we have completely disabled frame and line
interrupts, and their handlers will NOT be called.

[Question-Mark-Dude1-300x300]Questions and Answers

:? Cho music when lowercase spoils?\\
! Alas, the standard pt3 player uses the stack in operation, and when
calling the line processor, the data at the address in the SP is
replaced with its call address. And spoil the muse data.

Accordingly, we cause the player to turn off the lowercase ones (for
example, outside the screen, set up personnel), or use the psg player.
... or playing a non-stackable player.

? And how do you play with fullscreen effects?\\
! 1) Heh, lowercase interrupts are called every line. Accordingly, we
obtain a lower frequency frequency of 15625 hertz, and each line is
thrown into the audio coking port. Naman? And do not forget the
previous question\\
2) The case, if only the 15625 sound, and the personnel interruption
is long enough, then allow (nested) interrupts in the personnel
handler. Plus, you can move the personnel int (for example) to the
middle of the line, so that they would not come at the same time as
the lower case.
